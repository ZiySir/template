plugins {
    id 'java'
    id 'java-library'
    id 'com.palantir.baseline-idea'
    // 代码静态检查插件
    id 'com.palantir.baseline-error-prone'

    // 引入nullaway进行Jspecity分析检查（这里采用了baseline提出的封装引入，避免手动引入uber.nullaway导致的兼容问题）
    id 'com.palantir.baseline-null-away'

    // 代码格式检查插件
    id 'com.palantir.baseline-checkstyle'

    // 代码自动格式化插件
    id 'com.diffplug.spotless'
    id 'com.palantir.java-format' // palantir风格

    id 'com.palantir.baseline-encoding'
    id 'com.palantir.baseline-exact-dependencies' // 帮助优化依赖关系
}

description = "palantir baseline代码规范化插件"

def safeLoggingVersion = '3.7.0'

dependencies {
    // 使用更安全的日志记录: https://github.com/palantir/safe-logging
    implementation "com.palantir.safe-logging:logger:${safeLoggingVersion}"
    implementation "com.palantir.safe-logging:preconditions:${safeLoggingVersion}"
    testImplementation "com.palantir.safe-logging:preconditions-assertj:${safeLoggingVersion}"

    // JSpecify: null检查
    implementation "org.jspecify:jspecify:1.0.0"
}

// 代码自动格式化配置.
spotless {
    java {
        lineEndings com.diffplug.spotless.LineEnding.UNIX
        endWithNewline()
        indentWithSpaces 4
        trimTrailingWhitespace()
        custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
    }
    groovyGradle {
        greclipse()
    }
}

// 将所有警告转换为错误，进一步强制规范代码
tasks.withType(JavaCompile).configureEach {
    options.errorprone.disable 'StrictUnusedVariable'

    options.compilerArgs += ['-Werror']
}
