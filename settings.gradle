pluginManagement {
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
	}
	plugins {
		id("io.spring.dependency-management") version settings.prop("io.spring.dependency-management.version")
		id("org.springframework.boot") version settings.prop("org.springframework.boot.version")

		def baselineVersionPropertyName = 'com.palantir.baseline.version'
		// baseline support
		id("com.palantir.baseline-idea") version settings.prop("com.palantir.baseline-idea.version", baselineVersionPropertyName)
		id("com.palantir.baseline-config") version settings.prop("com.palantir.baseline-config.version", baselineVersionPropertyName)
		// baseline 代码规范
		id("com.palantir.baseline-error-prone") version settings.prop("com.palantir.baseline-error-prone.version", baselineVersionPropertyName)
		id("com.palantir.baseline-checkstyle") version settings.prop("com.palantir.baseline-checkstyle.version", baselineVersionPropertyName)
		id("com.palantir.java-format") version settings.prop("com.palantir.java-format.version")
		// spotless
		id("com.diffplug.spotless") version settings.prop("com.diffplug.spotless.version")
		// baseline 检查依赖关系
		id("com.palantir.baseline-exact-dependencies") version prop("com.palantir.baseline-exact-dependencies.version", baselineVersionPropertyName)
		id("com.palantir.baseline-encoding") version prop("com.palantir.baseline-encoding.version", baselineVersionPropertyName)
	}
}


rootProject.name = rootDir.name

include("example:demo1")

rootProject.children.each { p ->
	walk(p) { project, name ->
		project.buildFileName = "${name}.gradle"
		if (!project.buildFile.exists()) {
			new File(project.projectDir, "build.gradle").with { buildGradleFile ->
				if (buildGradleFile.exists()) {
					buildGradleFile.renameTo(project.buildFile)
				}
			}
		}
		println "project name: ${project.name}\tbuild file: ${project.buildFileName}"
	}
}

static def walk(ProjectDescriptor project, Closure cb) {
	def projectName = project.name.split(":").last()
	cb.call(project, projectName)
	project.children.each { subProject ->
		walk(subProject, cb)
	}
}

def prop(String prop, String defaultPropertyName = null) {
	def value = getProperty(prop)
	if (value == null && defaultPropertyName != null) {
		value = getProperty(defaultPropertyName)
	}
	return value
}